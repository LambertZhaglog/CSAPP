#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len ints to dst.
# Return the number of positive ints (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
ncopy:	pushl %ebp		# Save old frame pointer
	rrmovl %esp,%ebp	# Set up new frame pointer
	pushl %esi		# Save callee-save regs
	pushl %ebx
	pushl %edi
	mrmovl 8(%ebp),%ebx	# src
	mrmovl 16(%ebp),%edx	# len
	mrmovl 12(%ebp),%ecx	# dst

##################################################################
# You can modify this portion
	# Loop header
	xorl %eax, %eax		# count = 0;
	rrmovl %edx, %edi
	iaddl $-1, %edi		# len>1?
#	andl %edx, %edx		# len > 0?
#	jg Loop			# for remove mispredict
#	jmp Done
	jg Loop
	jmp Tag
	

Loop:	mrmovl (%ebx), %esi	# read val from src...
	rrmovl %eax, %edi
	rmmovl %esi, (%ecx)	# ...and store it to dst
	iaddl  $1, %edi
	andl %esi, %esi		# val <= 0?
	cmovg %edi, %eax
	
	mrmovl 4(%ebx), %esi	# read val from src...
	rrmovl %eax, %edi
	rmmovl %esi, 4(%ecx)	# ...and store it to dst
	iaddl  $1, %edi
	andl %esi, %esi		# val <= 0?
	cmovg %edi, %eax
	
	iaddl $8, %ecx		# dst++
	iaddl $8, %ebx		# src++
	iaddl $-2, %edx		# len--, and len > 0?
	rrmovl %edx, %edi
	iaddl $-1, %edi
	jg Loop			# if so, goto Loop:

Tag:	andl %edx, %edx
	jle Done
	mrmovl (%ebx), %esi	# read val from src...
	rrmovl %eax, %edi
	rmmovl %esi, (%ecx)	# ...and store it to dst
	iaddl  $1, %edi
	andl %esi, %esi		# val <= 0?
	cmovg %edi, %eax

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	popl %edi               # Restore callee-save registers
	popl %ebx
	popl %esi
	rrmovl %ebp, %esp
	popl %ebp
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
